apply from: 'dependencies.gradle'
apply from: 'checkstyle.gradle'

buildscript {
    ext.kotlinVersion = '1.3.31'
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.3.2'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.21.0'
        classpath 'org.codehaus.groovy:groovy-android-gradle-plugin:2.0.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

allprojects {
    apply plugin: 'com.github.ben-manes.versions'
    
    repositories {
        google()
        jcenter()
        maven { url 'https://jitpack.io' }
    }

    buildscript {
        repositories {
            google()
            jcenter()
        }
    }

    if (System.env.TRAVIS == 'true') {
        tasks.withType(GroovyCompile) {
            groovyOptions.fork = false
        }
        tasks.withType(Test) {
            // containers (currently) have 2 dedicated cores and 4GB of memory
            maxParallelForks = 2
            minHeapSize = '128m'
        }
    }

    dependencyUpdates.resolutionStrategy {
        componentSelection { rules ->
            rules.all { ComponentSelection selection ->
                boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview'].any { qualifier ->
                    selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
                }
                if (rejected) {
                    selection.reject('Release candidate')
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

apply plugin: 'maven-publish'

def getArtificatId = { ->
    return "rxandroidble"
}

publishing {
    publications {
        app(MavenPublication) {
            groupId 'com.github.rxandroidble'
            artifactId getArtificatId()
            version "1.0"

            artifact('/Users/rohan/repos/RxAndroidBle/rxandroidble/build/outputs/aar/rxandroidble-debug.aar') {}
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/proxyco/RxAndroidBle")
            credentials {
                username = System.getenv("GPR_USER")
                password = System.getenv("GPR_API_KEY")
            }
        }
    }
}