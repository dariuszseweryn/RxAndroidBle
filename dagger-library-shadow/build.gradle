buildscript {
    dependencies {
        classpath "gradle.plugin.com.github.johnrengelman:shadow:$rootProject.ext.shadowPluginVersion"
    }
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'
apply plugin: 'com.vanniktech.maven.publish'
apply from: rootProject.file('gradle/shadow.gradle')
apply from: rootProject.file('gradle/gradle-mvn-config.gradle')

import com.github.jengelman.gradle.plugins.shadow.transformers.ServiceFileTransformer
shadowJar {
    mustRunAfter jar
    transform(ServiceFileTransformer)
    classifier = ''
}

artifacts {
    archives shadowJar
}

shadowJar {
    // Dagger 2.25.4 introduced a file for tracking purposes. This can break apps using Dagger.
    // See https://github.com/dariuszseweryn/RxAndroidBle/issues/789
    // Source https://github.com/google/dagger/commit/709098caaf4c7124f5e5313c1aa9ab34fced0031
    exclude('META-INF/com.google.dagger_dagger.version')
}

dependencies {
    implementation rootProject.ext.libs.dagger
}

java {
    sourceCompatibility = rootProject.ext.sourceCompatibilityVersion
    targetCompatibility = rootProject.ext.targetCompatibilityVersion
}

task javadocs(type: Javadoc) {
    source = sourceSets.main.java.source
    classpath += configurations.compileClasspath
}

task javadocsJar(type: Jar, dependsOn: javadocs) {
    archiveClassifier.set('javadoc')
    from javadocs.destinationDir
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}
