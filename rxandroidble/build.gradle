apply plugin: 'com.android.library'
apply plugin: 'groovyx.android'
apply from: rootProject.file('gradle/gradle-mvn-push.gradle')

android {
    compileSdkVersion 25

    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 25
        versionCode 1
        versionName VERSION_NAME
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility rootProject.ext.targetCompatibilityVersion
        targetCompatibility rootProject.ext.targetCompatibilityVersion
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.all {
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen { false }
                showStandardStreams = true
            }
        }
    }

    lintOptions {
        abortOnError false
    }

    preBuild.dependsOn 'checkstyle'
}

// Workaround until groovy-android-gradle-plugin 1.3.0 is released
tasks.whenTaskAdded { task ->
    if (task.name.contains('UnitTestGroovyWithGroovyc')) {
        task.doFirst {
            ['debug','release'].each {
                def dir = file("build/generated/source/apt/test/$it")
                if(!dir.isDirectory()){
                    dir.mkdirs()
                }
            }
        }
    }
}

dependencies {
    api rootProject.ext.libs.rxjava
    api rootProject.ext.libs.support_annotations
    implementation rootProject.ext.libs.rxrelay
    implementation rootProject.ext.libs.dagger
    annotationProcessor rootProject.ext.libs.dagger_compiler

    // Test dependencies
    testCompileOnly rootProject.ext.libs.junit
    testCompileOnly rootProject.ext.libs.groovy
    testCompileOnly rootProject.ext.libs.spock
    testCompileOnly rootProject.ext.libs.robospock
    testCompileOnly rootProject.ext.libs.support
}
