apply plugin: 'com.android.library'
apply plugin: 'groovyx.android'
apply from: rootProject.file('gradle/gradle-mvn-push.gradle')

android {
    compileSdkVersion 31

    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 31
        versionCode 1
        versionName VERSION_NAME
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            consumerProguardFiles 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility rootProject.ext.targetCompatibilityVersion
        targetCompatibility rootProject.ext.targetCompatibilityVersion
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.all {
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen { false }
                showStandardStreams = true
            }
        }
    }

    lintOptions {
        abortOnError false
    }

    preBuild.dependsOn 'checkstyle'
}

androidGroovy {
    skipJavaC = true
}

dependencies {
    api rootProject.ext.libs.rxjava
    api rootProject.ext.libs.annotations
    implementation rootProject.ext.libs.rxrelay
    implementation project(path: ':dagger-library-shadow', transitive: false, configuration: 'shadow')
    annotationProcessor project(path: ':dagger-compiler-shadow', transitive: false, configuration: 'shadow')

    // Test dependencies
    testImplementation project(path: ':dagger-library-shadow', transitive: false, configuration: 'shadow')
    testImplementation rootProject.ext.libs.junit
    testImplementation rootProject.ext.libs.groovy
    testImplementation rootProject.ext.libs.spock
    testImplementation(rootProject.ext.libs.robolectric)
    testImplementation(rootProject.ext.libs.electricspock){
        exclude group: 'org.robolectric', module: 'android-all'
    }
}
