apply plugin: 'com.android.library'
apply plugin: 'groovyx.android'
apply plugin: 'com.vanniktech.maven.publish'
apply from: rootProject.file('gradle/gradle-mvn-config.gradle')

android {
    namespace 'com.polidea.rxandroidble2'

    compileSdkVersion 32

    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 32
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            consumerProguardFiles 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility rootProject.ext.targetCompatibilityVersion
        targetCompatibility rootProject.ext.targetCompatibilityVersion
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.all {
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen { false }
                showStandardStreams = true
            }
        }
    }

    lint {
        abortOnError false
    }

    preBuild.dependsOn 'checkstyle'

    packagingOptions {
        // Dagger 2.25.4 introduced a file for tracking purposes. This can break apps using Dagger.
        // See https://github.com/dariuszseweryn/RxAndroidBle/issues/789
        // Source https://github.com/google/dagger/commit/709098caaf4c7124f5e5313c1aa9ab34fced0031
        excludes = ['**/com.google.dagger_dagger.version']
    }
}

apply from: rootProject.file('gradle/rxjava3-generation.gradle')

androidGroovy {
    skipJavaC = true
}

afterEvaluate { project ->
    // add explicit dependencies to compile groovy when testing
    testDebugUnitTest.dependsOn 'compileDebugUnitTestGroovyWithGroovyc'
    testReleaseUnitTest.dependsOn 'compileReleaseUnitTestGroovyWithGroovyc'
}

dependencies {
    api rootProject.ext.libs.rxjava2
    api rootProject.ext.libs.annotations
    implementation rootProject.ext.libs.rxrelay2
    implementation project(path: ':dagger-library-shadow', transitive: false, configuration: 'shadow')
    annotationProcessor project(path: ':dagger-compiler-shadow', transitive: false, configuration: 'shadow')

    compileOnly files(tasks.getByPath(':dagger-library-shadow:shadowJar').archiveFile)

    // Test dependencies
    testImplementation rootProject.ext.libs.junit
    testImplementation rootProject.ext.libs.groovy
    testImplementation rootProject.ext.libs.spock
}
